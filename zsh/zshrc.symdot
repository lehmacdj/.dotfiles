# Set the location of the dotfiles repository
# absolute path of currently sourced file; go up two levels to get to repo root
export DOTFILES="${${(%):-%x}:A:h:h}"
[ -n "$PROFILE_ZSH" ] && source "$DOTFILES/zsh/start-profiling.zsh" || true

if [ -f "$_zplug_lock[job]" ]; then
    >&2 echo "zplug lock file is leftover, this may inhibit job control (e.g. fg/bg)"
    >&2 echo 'you might want to remove it manually: `rm $_zplug_lock[job]`'
fi

source "$HOME/.zplug/init.zsh"
# some things depend on env.sh, so we need to load it first
zplug "$DOTFILES/shell", from:"local", use:"env.sh", defer:0

zplug "$DOTFILES/shell", from:"local", use:"{aliases,fzf}.sh", defer:1
zplug "$DOTFILES/zsh/autoload", from:"local", defer:1
zplug "zsh-users/zsh-history-substring-search", defer:1
zplug "mafredri/zsh-async", from:"github", use:"async.zsh", defer:1
zplug 'zplug/zplug', hook-build:'zplug --self-manage', defer:1

# after compinit
zplug "zsh-users/zsh-syntax-highlighting", defer:2
zplug "$DOTFILES/zsh", from:"local", use:"post_compinit.zsh", defer:2

# check if there are plugins that need to be installed
if ! zplug check --verbose; then
    printf "Install? [y/N]: "
    if read -q; then
        >&2 echo; zplug install
    fi
    # on first load, show loading
    zplug load --verbose
    [ -f "$HOME/.local.zsh" ] && source "$HOME/.local.zsh"
else
    # otherwise just let the plugins go, with instant prompt loading
    source "$DOTFILES/zsh/instant-zsh.zsh"
    instant-zsh-pre $'\e[6 q\e[32m‚ùØ \e[0m' # set line cursor, then green basic prompt
    zplug load
    [ -f "$HOME/.local.zsh" ] && source "$HOME/.local.zsh"
    instant-zsh-post
fi

[ -n "$PROFILE_ZSH" ] && source "$DOTFILES/zsh/stop-profiling.zsh" || true
